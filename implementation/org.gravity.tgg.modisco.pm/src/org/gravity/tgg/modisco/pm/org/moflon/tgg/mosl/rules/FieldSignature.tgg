#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#abstract #rule FieldSignature_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	mFieldName:MFieldName {
		++-mSignatures -> mSignature
	}

	++mSignature:MFieldSignature

	mType:Type
}

#target {
	tField:TField {
		++-signatures -> tSignature
	}

	++tSignature:TFieldSignature {
		++  -type -> tType
	}

	tType:TAbstractType
}

#correspondence {
	eFieldDeclarationToTField :MFieldNameToTField {
		#src->mFieldName
		#trg->tField
	}

	++ mSignatureToTSignature :MSignatureToTSignature {
		#src->mSignature
		#trg->tSignature
	}

	++mFlowElementToTFlowElement:MAbstractFlowElementToTAbstractFlowElement {
		#src->mSignature
		#trg->tSignature
	}

	mTypeToTType :TypeToTAbstractType {
		#src->mType
		#trg->tType
	}
}


#using org.gravity.tgg.modisco.pm.*

#rule FieldSignature #extends FieldSignature_Abstract
#with org.gravity.tgg.modisco.pm

#source{
	++mSignature:MFieldSignature{
		++  -type -> mType
	}

	mType:Type
}

#target {
	++tSignature:TFieldSignature {
		lowerBound := 0
		upperBound := 1
	}
}


#using org.gravity.tgg.modisco.pm.*

#rule FieldSignatureParameterizedType #extends FieldSignature_Abstract
#with org.gravity.tgg.modisco.pm

#source{
	++mSignature:MFieldSignature{
		++  -type -> mParameterizedType
	}

	mParameterizedType:ParameterizedType {
		-type -> mTypeAccess
	}

	mTypeAccess:TypeAccess {
		-type -> mType
	}

	mType:Type
}

#target {
	++tSignature:TFieldSignature {
		lowerBound := 0
		upperBound := 1
	}
}


#using org.gravity.tgg.modisco.pm.*

#rule FieldSignatureArrayType #extends FieldSignature_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mSignature:MFieldSignature{
		++  -type -> mArrayType
	}

	mArrayType:ArrayType {
		-elementType -> mTypeAccess
	}

	mTypeAccess:TypeAccess {
		-type -> mType
	}

	mType:Type
}

#target {
	++tSignature:TFieldSignature {
		lowerBound := 0
		upperBound := -1
	}
}


