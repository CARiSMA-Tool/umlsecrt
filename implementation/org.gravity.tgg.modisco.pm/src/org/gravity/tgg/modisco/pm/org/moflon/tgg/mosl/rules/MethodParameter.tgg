#using org.gravity.tgg.modisco.pm.*
#using AttrCondDefLibrary.*

#abstract #rule MethodParameter_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	mSignature:MMethodSignature {
		++  -mEntrys -> mEntry
	}

	++mEntry:MEntry

	mType:Type
}

#target {
	tSiganture:TMethodSignature {
		++  -parameters -> tParameter
	}

	++tParameter:TParameter {
		++  -type -> tAbstractType
	}

	tAbstractType:TAbstractType
}

#correspondence {
	mSignatureToTSignature :MSignatureToTSignature {
		#src->mSignature
		#trg->tSiganture
	}

	++ mEntryToTParameter :MEntryToTParameter {
		#src->mEntry
		#trg->tParameter
	}

	++mFlowElementToTFlowElement:MAbstractFlowElementToTAbstractFlowElement {
		#src->mEntry
		#trg->tParameter
	}

	mTypeToTAbstractType :TypeToTAbstractType {
		#src->mType
		#trg->tAbstractType
	}
}


#using org.gravity.tgg.modisco.pm.*

#rule MethodParameterSingleType #extends MethodParameter_Abstract
#with org.gravity.tgg.modisco.pm

#source{
	++mEntry:MEntry{
		++  -type -> mType
	}

	mType:Type
}

#target{
	++tParameter:TParameter {
		lowerBound := 0
		upperBound := 1
	}
}


#using org.gravity.tgg.modisco.pm.*

#rule MethodParameterArrayType #extends MethodParameter_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mEntry:MEntry{
		++  -type -> mArrayType
	}

	mArrayType:ArrayType {
		-elementType -> mElementTypeAccess
	}

	mElementTypeAccess:TypeAccess{
		-type -> mType
	}

	mType:Type
}

#target {
	++tParameter:TParameter {
		lowerBound := 0
		upperBound := -1
	}
}


#using org.gravity.tgg.modisco.pm.*

#rule MethodParameterParameterizedType #extends MethodParameter_Abstract
#with org.gravity.tgg.modisco.pm

#source {
	++mEntry:MEntry{
		++  -type -> mParameterizedType
	}

	mParameterizedType:ParameterizedType {
		-type -> mElementTypeAccess
	}

	mElementTypeAccess:TypeAccess{
		-type -> mType
	}

	mType:Type
}

#target {
	++tParameter:TParameter {
		lowerBound := 0
		upperBound := -1
	}
}


