#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#abstract #rule FieldAccess
#with org.gravity.tgg.modisco

#source {
	mMember:MDefinition {
		++  -mAbstractFieldAccess -> mAccess
	}

	++mAccess:MSingleVariableAccess {
		++  -variable -> varDeclFrag
	}

	varDeclFrag:VariableDeclarationFragment {
		-variablesContainer -> mField
	}

	mField:MFieldDefinition {
		-abstractTypeDeclaration -> mType
	}

	mType:AbstractTypeDeclaration
}

#target {
	tMember:TMember {
		++  -tAccessing -> tAccess
	}

	++tAccess:TAccess {
		++-tTarget -> tField
	}

	tField:TFieldDefinition {
		-definedBy -> tType
	}

	tType:basic.TAbstractType
}

#correspondence {
	mMemberToTMember :MDefinitionToTMember {
		#src->mMember
		#trg->tMember
	}

	mMethodToTMethod :MDefinitionToTMember {
		#src->mField
		#trg->tField
	}

	mTypeToTType :TypeToTAbstractType {
		#src->mType
		#trg->tType
	}

	++ mAccessToTAccess :MSingleVariableAccessToTAccess {
		#src->mAccess
		#trg->tAccess
	}
}

#rule FieldAccessRead #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess {
		++	-outgoingFlows -> outMFlow
	}
	
	++outMFlow:MFlow {
		++	-flowTarget -> mTarget
	}
	
	mTarget:MAbstractFlowElement
}

#target { 
	++tAccess:TRead {
		++	-outgoingFlows -> outTFlow
	}
	

	++outTFlow:TFlow {
		++ -flowTarget -> tTarget
	}
	
	tTarget:TAbstractFlowElement
}

#correspondence {
	++outMFlowToOutTFlow:MFlowToTFlow {
		#src->outMFlow
		#trg->outTFlow
	}
	
	mTargetToTTarget:MAbstractFlowElementToTAbstractFlowElement {
		#src->mTarget
		#trg->tTarget
	}
}

#rule FieldAccessWrite #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess {
		++	-incomingFlows -> inMFlow
	}
	
	++inMFlow:MFlow {
		++	-flowSource -> mSource
	}
	
	mSource:MAbstractFlowElement
}

#target { 
	++tAccess:TWrite {
		++	-incomingFlows -> inTFlow
	}
	
	++inTFlow:TFlow {
		++ -flowSource -> tSource
	}
	
	tSource:TAbstractFlowElement
}

#correspondence {
	++outMFlowToOutTFlow:MFlowToTFlow {
		#src->inMFlow
		#trg->inTFlow
	}
	
	mSourceToTSource:MAbstractFlowElementToTAbstractFlowElement {
		#src->mSource
		#trg->tSource
	}
}

#rule FieldAccessReadWrite #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess {
		++	-incomingFlows -> inMFlow
		++	-outgoingFlows -> outMFlow
	}
	
	++inMFlow:MFlow {
		++	-flowSource -> mSource
	}
	
	++outMFlow:MFlow {
		++ -flowTarget -> mTarget
	}
	
	mSource:MAbstractFlowElement
	
	mTarget:MAbstractFlowElement
}

#target { 
	++tAccess:TReadWrite {
		++	-incomingFlows -> inTFlow
		++	-outgoingFlows -> outTFlow
	}
	
	++inTFlow:TFlow {
		++ -flowSource -> tSource
	}
	
	++outTFlow:TFlow {
		++ -flowTarget -> tTarget
	}
	
	tSource:TAbstractFlowElement
	
	tTarget:TAbstractFlowElement
}

#correspondence {
	++outMFlowToOutTFlow:MFlowToTFlow {
		#src->inMFlow
		#trg->inTFlow
	}
	
	mSourceToTSource:MAbstractFlowElementToTAbstractFlowElement {
		#src->mSource
		#trg->tSource
	}
	
	mTargetToTTarget:MAbstractFlowElementToTAbstractFlowElement {
		#src->mTarget
		#trg->tTarget
	}
}

