#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#abstract #rule FieldAccess
#with org.gravity.tgg.modisco

#source {
	mMember:MDefinition {
		++  -mAbstractFieldAccess -> mAccess
	}

	++mAccess:MSingleVariableAccess {
		++  -variable -> varDeclFrag
	}

	varDeclFrag:VariableDeclarationFragment {
		-variablesContainer -> mField
	}

	mField:MFieldDefinition {
		-abstractTypeDeclaration -> mType
	}

	mType:AbstractTypeDeclaration
}

#target {
	tMember:TMember {
		++  -tAccessing -> tAccess
	}

	++tAccess:TAccess {
		++-tTarget -> tField
	}

	tField:TFieldDefinition {
		-definedBy -> tType
	}

	tType:basic.TAbstractType
}

#correspondence {
	mMemberToTMember :MDefinitionToTMember {
		#src->mMember
		#trg->tMember
	}

	mMethodToTMethod :MDefinitionToTMember {
		#src->mField
		#trg->tField
	}

	mTypeToTType :TypeToTAbstractType {
		#src->mType
		#trg->tType
	}

	++ mAccessToTAccess :MSingleVariableAccessToTAccess {
		#src->mAccess
		#trg->tAccess
	}
}

#rule FieldAccessRead #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess
}

#target { 
	++tAccess:TRead
}

#correspondence {
}

#rule FieldAccessWrite #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess
}

#target { 
	++tAccess:TWrite
}

#correspondence {
	
}

#rule FieldAccessReadWrite #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess
}

#target { 
	++tAccess:TReadWrite
}

#correspondence {
	
}
