#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#abstract #rule FieldAccess
#with org.gravity.tgg.modisco

#source {
	mMember:MDefinition {
		++  -mAbstractFieldAccess -> mAccess
	}

	++mAccess:MSingleVariableAccess {
		++  -variable -> varDeclFrag
	}

	varDeclFrag:VariableDeclarationFragment {
		-variablesContainer -> mField
	}

	mField:MFieldDefinition {
		-abstractTypeDeclaration -> mType
	}

	mType:AbstractTypeDeclaration
}

#target {
	tMember:TMember {
		++  -tAccessing -> tAccess
	}

	++tAccess:TAccess {
		++-tTarget -> tField
	}

	tField:TFieldDefinition {
		-definedBy -> tType
	}

	tType:basic.TAbstractType
}

#correspondence {
	mMemberToTMember :MDefinitionToTMember {
		#src->mMember
		#trg->tMember
	}

	mMethodToTMethod :MDefinitionToTMember {
		#src->mField
		#trg->tField
	}

	mTypeToTType :TypeToTAbstractType {
		#src->mType
		#trg->tType
	}

	++ mAccessToTAccess :MSingleVariableAccessToTAccess {
		#src->mAccess
		#trg->tAccess
	}
}

#abstract #rule FieldAccessRead #extends FieldAccess
#with org.gravity.tgg.modisco

#source {
	++mAccess:MSingleVariableAccess {
		++	-outgoingFlows -> outMFlow
	}
	
	++outMFlow:MFlow {
		++	-flowTarget -> mTarget
	}
	
	++mTarget:MAbstractFlowElement
}

#target { 
	++tAccess:TAccess {
		++	-outgoingFlows -> outTFlow
	}
	

	++outTFlow:TFlow {
		++ -flowTarget -> tTarget
	}
	
	++tTarget:TAbstractFlowElement

	//TODO Change TAccess to TRead
}

#correspondence {
	++outMFlowToOutTFlow:MFlowToTFlow {
		#src->outMFlow
		#trg->outTFlow
	}
	
	++mTargetToTTarget:MAbstractFlowElementToTAbstractFlowElement {
		#src->mTarget
		#trg->tTarget
	}
}




