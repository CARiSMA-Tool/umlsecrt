#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#abstract #rule MethodParameter
#with org.gravity.tgg.modisco

#source {
	mSignature:MMethodSignature {
		++  -mEntrys -> mEntry
	}

	++mEntry:MEntry{
		++  -type -> mType
	}

	mType:Type
}

#target {
	tSiganture:TMethodSignature {
		++  -parameters -> tParameter
	}

	++tParameter:TParameter {
		++  -type -> tAbstractType
	}

	tAbstractType:TAbstractType
}

#correspondence {
	++ mEntryToTParameter :MEntryToTParameter {
		#src->mEntry
		#trg->tParameter
	}

	++mFlowElementToTFlowElement:MAbstractFlowElementToTAbstractFlowElement {
		#src->mEntry
		#trg->tParameter
	}

	mSignatureToTSignature :MSignatureToTSignature {
		#src->mSignature
		#trg->tSiganture
	}
}

#rule MethodParameterSingleType #extends MethodParameter
#with org.gravity.tgg.modisco

#correspondence {
	mTypeToTAbstractType :TypeToTAbstractType {
		#src->mType
		#trg->tAbstractType
	}
}

#rule MethodParameterArrayType #extends MethodParameter
#with org.gravity.tgg.modisco

#source {
	mType:ArrayType{
		-elementType -> mTypeAccess
	}

	mTypeAccess:TypeAccess {
		-type -> mTypeOfArray
	}

	mTypeOfArray:Type
}

#target {
	++ tParameter :TParameter {
		lowerBound := 0
		upperBound := -1
	}
}

#correspondence {
	mTypeToTAbstractType :TypeToTAbstractType {
		#src->mTypeOfArray
		#trg->tAbstractType
	}
}