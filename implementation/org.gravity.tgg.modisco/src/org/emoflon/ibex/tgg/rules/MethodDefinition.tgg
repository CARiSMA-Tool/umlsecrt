#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#abstract #rule AbstractMethodDefinition #with org.gravity.tgg.modisco

#source {
	mSignature:MMethodSignature {
		++  -mMethodDefinitions -> mDefinition
		++  -mDefinitions -> mDefinition
	}

	++mDefinition:MAbstractMethodDefinition
}

#target {
	++tDefinition:TMethodDefinition

	tMethodSignature:TMethodSignature {
		++  -definitions -> tDefinition
	}
}

#correspondence {
	++ mDefinitionToTMember :MDefinitionToTMember {
		#src->mDefinition
		#trg->tDefinition
	}

	++mFlowElementToTFlowElement:MAbstractFlowElementToTAbstractFlowElement {
		#src->mDefinition
		#trg->tDefinition
	}

	mSignatureToTMethodSignature :MSignatureToTSignature {
		#src->mSignature
		#trg->tMethodSignature
	}

	++ mBodyToTAnnotation :ASTNodeToTAnnotatable {
		#src->mDefinition
		#trg->tDefinition
	}
}

#rule MethodDefinition #extends AbstractMethodDefinition
#with org.gravity.tgg.modisco

#source {
	++mDefinition:MMethodDefinition
}

#rule ConstructorDefinition #extends AbstractMethodDefinition
#with org.gravity.tgg.modisco

#source {
	++mDefinition:MConstructorDefinition
}

#target {
	++tDefinition:TMethodDefinition {
		++-tAnnotation -> tAnnotation
	}

	++tAnnotation:TConstructor
}


