#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#rule AnonymousClass
#with org.gravity.tgg.modisco

#source {
	mPackage : Package
	mType : AbstractTypeDeclaration {
		-package->mPackage
	}
	++ mAnonymous : MAnonymous {
		++-mSourroundingType->mType
	}
	mModel : MGravityModel {
		++  -anonymousClassDeclarations -> mAnonymous
	}
}

#target {
	tPackage : TPackage 
	tType : TAbstractType {
		-package->tPackage
		++-innerTypes->tAnonymous
	}
	++ tNewPackage : TPackage {
		++-ownedTypes->tAnonymous
		++-parent->tPackage
	}
	++ tAnonymous : TClass {
		tLib := false
		tName := "Anonymous"
	}
	pg : TypeGraph {
		++  -classes -> tAnonymous
		++  -ownedTypes -> tAnonymous
	}
}

#correspondence {
	++ eAnonymousClassDeclarationToTClass : AnonymousClassDeclarationToTClass {
		#src->mAnonymous
		#trg->tAnonymous
	}
	eModelToPg : ModelToTypeGraph {
		#src->mModel
		#trg->pg
	}
	mTypeToTType : TypeToTAbstractType {
		#src->mType
		#trg->tType
	}
	mPackageToTPackage : PackageToTPackage {
		#src->mPackage
		#trg->tPackage
	}
}

#attributeConditions {
	addSuffix(tType.tName, "$1", tNewPackage.tName)
}