#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#rule MethodInvocationStaticType #with org.gravity.tgg.modisco

#source {
	mOwner:MClass {
		-bodyDeclarations -> mMember
	}

	mMember:MDefinition {
		-abstractMethodInvocations -> invocation
		++-invocationStaticTypes -> staticTypeInvoc
	}

	invocation:AbstractMethodInvocation {
		-method -> mCalled
	}

	++staticTypeInvoc:MethodInvocationStaticType{
		++ -methodInvoc -> invocation
		++ -type -> mStaticType
	}

	mCalled:MMethodDefinition

	mStaticType:Type
}

#target {
	tOwner:TAbstractType {
		-defines -> tMember
	}

	tMember:TMember {
		-tAccessing -> tAccess
	}

	tAccess:TCall {
		++-staticType -> tStaticType
		-tTarget -> tCalled
	}

	tCalled:TMethodDefinition

	tStaticType:TAbstractType
}

#correspondence {
	mOwnerToTOwner :TypeToTAbstractType {
		#src->mOwner
		#trg->tOwner
	}

	invocationToTAccess :AbstractMethodInvocationToTAccess {
		#src->invocation
		#trg->tAccess
	}

	mMemberToTMember :MDefinitionToTMember {
		#src->mMember
		#trg->tMember
	}

	mCalledToTCalled :MDefinitionToTMember {
		#src->mCalled
		#trg->tCalled
	}

	mStaticTypeTotStaticType :TypeToTAbstractType {
		#src->mStaticType
		#trg->tStaticType
	}

	++staticTypeInvocationToAccessStaticType:StaticTypeInvocationToAccessStaticType{
		#src->staticTypeInvoc
		#trg->tAccess
	}
}


