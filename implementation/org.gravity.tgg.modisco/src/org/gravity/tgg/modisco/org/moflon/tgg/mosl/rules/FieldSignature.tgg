#using org.gravity.tgg.modisco.*
#using AttrCondDefLibrary.*

#abstract #rule FieldSignatureAbstract
#with org.gravity.tgg.modisco

#source {
	mFieldName:MFieldName {
		++-mSignatures -> mSignature
	}

	++mSignature:MFieldSignature{
		++  -type -> mType
	}

	mType:Type
}

#target {
	tField:TField {
		++-signatures->tSignature
	}

	++tSignature:TFieldSignature {
		++  -type -> tType
	}
	
	tType:TAbstractType
}

#correspondence {
	eFieldDeclarationToTField :MFieldNameToTField {
		#src->mFieldName
		#trg->tField
	}

	++ mSignatureToTSignature :MSignatureToTSignature {
		#src->mSignature
		#trg->tSignature
	}

	++mFlowElementToTFlowElement:MAbstractFlowElementToTAbstractFlowElement {
		#src->mSignature
		#trg->tSignature
	}
}

#rule FieldSignature #extends FieldSignatureAbstract
#with org.gravity.tgg.modisco

#source{
	mType : Type
}

#target {
	tType : TAbstractType
}


#correspondence {
	mTypeToTType : org.gravity.tgg.modisco.TypeToTAbstractType {
		#src->mType
		#trg->tType
	}
}

#rule FieldSignatureArrayType #extends FieldSignatureAbstract
#with org.gravity.tgg.modisco

#source {
	mType : ArrayType {
		-elementType->mTypeAccess
	}
	
	mTypeAccess : TypeAccess {
		-type->mArrayType
	}
	
	mArrayType : Type
}

#target {
	tType : TAbstractType
	
	++ tSignature : TFieldSignature {
		upperBound := -1
	} 
}

#correspondence {	
	mTypeToTType : org.gravity.tgg.modisco.TypeToTAbstractType {
		#src->mArrayType
		#trg->tType
	}
}